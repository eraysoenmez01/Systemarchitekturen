plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.5'
}

group 'at.fhv.lab2'
version '1.0-SNAPSHOT'

def grpcVersion = '1.57.1'
def protobufVersion = '3.23.4'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // gRPC Core
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"

    // Logging
    implementation 'ch.qos.logback:logback-classic:1.5.18'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly   'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/proto'
        }
        java {
            srcDirs += "$buildDir/generated/source/proto/main/java"
            srcDirs += "$buildDir/generated/source/proto/main/grpc"
        }
    }
}

application {
    mainClass = 'at.fhv.sysarch.lab2.ordermanager.OrderManagingSystem'
}

tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

test {
    useJUnitPlatform()
}